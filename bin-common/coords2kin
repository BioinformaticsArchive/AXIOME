#!/usr/bin/awk -f

function flush() {
	if (p) {
		print "@balllist color=" color " radius=" radius " alpha=0.75 dimension=7 master={points} nobutton";
		print p;
		print "@labellist color=" color " radius=" radius " alpha=0.75 dimension=7 master={labels} nobutton";
		print p;
	}
	p = "";
}

BEGIN {
	print "@kinemage {unlabelledimage}";
	color = 0;
}

$1 == "DIM" {
	printf("@dimension");
	for (i = 2; i <= NF; i++) {
		printf(" {%s}", $i);
	}
	printf("\n");
	printf("@master {points}\n@master {labels}\n@whitebackground\n@hsvcolor {black} 0.0 0.0 0.0\n");

	printf("@group {axes} collapsible\n");
	origin = "0"
	for (i = 3; i <= NF; i++) {
		origin = origin " 0";
	}
	for (i = 2; i <= NF; i++) {
		vector = (i == 2 ? "1" : "0");
		for (j = 3; j <= NF; j++) {
			vector = vector (j == i ? " 1" : " 0");
		}
		printf("@vectorlist {%s line} dimension=%d\n%s black\n%s black\n@labellist {%s} dimension=%d\n{%s} %s\n", $i, NF - 1, origin, vector, $i, NF - 1, $i, vector);
	}
}

$1 == "GROUP" {
	flush();
	color++;
	printf("@group {%s} collapsible\n", $2);
	color = $3
	radius = $4
}

$1 != "GROUP" && $1 != "DIM" {
	p = p "{" $1 "}";
	for (i = 2; i <= NF; i++) {
		p = p " " $i;
	}
	p = p "\n";
}

END {
	flush();
}
